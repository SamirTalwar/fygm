setopt no_global_rcs

if ! (( ${+LOADED_ZSHENV} )); then
  path=(
    /usr/local/bin
    /usr/local/sbin
    /usr/sbin
    /sbin
    $path
  )

  if [[ "$OSTYPE" == darwin* ]]; then
    export BROWSER='open'
    path=(
      ~/Library/Haskell/bin
      /Library/TeX/Distributions/.DefaultTeX/Contents/Programs/texbin
      $path
    )

    if (
      set -o nullglob
      PYTHON_3_DIRS=(~/Library/Python/3.*)
      [[ ${#PYTHON_3_DIRS} -eq 1 ]]
    ); then
      path=(~/Library/Python/3.*/bin $path)
    fi
  fi

  export EDITOR='vim'
  export VISUAL='vim'
  export PAGER='less'

  export GRADLE_OPTS="$GRADLE_OPTS -Dorg.gradle.daemon=true"

  if [[ -d /nix ]]; then
    source /nix/var/nix/profiles/default/etc/profile.d/nix-daemon.sh
  fi

  if [[ -d /usr/local/opt/ruby ]]; then
    path=(/usr/local/opt/ruby/bin $path)
  fi

  if [[ -d /usr/local/opt/fzf ]]; then
    path=(/usr/local/opt/fzf/bin $path)
  fi

  if [[ -d '/usr/local/Caskroom/google-cloud-sdk/latest/google-cloud-sdk' ]]; then
    source '/usr/local/Caskroom/google-cloud-sdk/latest/google-cloud-sdk/path.zsh.inc'
  fi

  path=(
    ~/.local/bin
    ~/.config/yarn/global/node_modules/.bin
    ~/.cargo/bin
    ~/.pulumi/bin
    ~/.yarn/bin
    ./node_modules/.bin
    /usr/local/opt/llvm/bin
    $path
  )

  path=(~/bin/unix $path)
  if [[ "$OSTYPE" == darwin* ]]; then
    path=(~/bin/mac $path)
  fi

  if [[ -e ~/.zshenv.local ]]; then
    source ~/.zshenv.local
  fi

  export LOADED_ZSHENV=true
fi
