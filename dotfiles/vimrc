set nocompatible " Disable vi support
set nobackup " Disable the backup file (ends in '~')

let mapleader=',' " The <Leader> in map operations is the ','
let maplocalleader=';' " The <LocalLeader> in map operations is the ';'

set encoding=utf-8 " Assume UTF-8

set number " Show line numbers
set colorcolumn=121 " Show the margin
set showcmd " Show the command at the bottom
set laststatus=2 " Always show the status bar at the bottom

set expandtab " Spaces, not tabs
set smartindent " Indent or dedent automatically when opening and closing blocks
set shiftwidth=4 " Indent and dedent
set tabstop=4 " The distance travelled by the Tab key after characters

set linebreak " Wrap at a word boundary (doesn't work in list mode)

set ignorecase " Ignore case when searching
set smartcase " Search case-insensitively until a capital letter is typed
set hlsearch " Highlight search results in the document

set mouse=a " Let the user use the mouse

command W w " Bind ':W' to ':w'
command Q q " Bind ':Q' to ':q'
command Wq wq " Bind ':Wq' to ':wq'
command WQ wq " Bind ':WQ' to ':wq'
" Move up and down according to the printed lines, including wrapping, not physical lines
nnoremap j gj
nnoremap k gk
set pastetoggle=<F2> " F2 toggles paste mode

" Set up plugins
call plug#begin()

" Sensible defaults
Plug 'tpope/vim-sensible'

" Solarized colour scheme
Plug 'altercation/vim-colors-solarized'

" Extra functionality in the status bar
Plug 'bling/vim-airline'
" Search files using Ctrl+P
Plug 'kien/ctrlp.vim'
" Search everything with :Ack
Plug 'mileszs/ack.vim'
" File/directory management
Plug 'scrooloose/nerdtree'
" EditorConfig support
Plug 'editorconfig/editorconfig-vim'
" Connect up tmux
Plug 'christoomey/vim-tmux-navigator'
" Interactive command execution
Plug 'Shougo/vimproc.vim', {'do': ':VimProcInstall'}

" Better repeat support with '.'
Plug 'tpope/vim-repeat'
" Insert, delete and change surrounding elements ('(', '[', '<', etc.)
Plug 'tpope/vim-surround'
" Comment with '\'
Plug 'tpope/vim-commentary'
" Move about with ','
Plug 'Lokaltog/vim-easymotion'
" Precision editing for S-expressions
Plug 'guns/vim-sexp'
" Autocompletion
Plug 'Shougo/deoplete.nvim', {'do': ':UpdateRemotePlugins'}

Plug 'plasticboy/vim-markdown'
Plug 'ensime/ensime-vim'
Plug 'derekwyatt/vim-scala'
Plug 'guns/vim-clojure-static'
Plug 'fsharp/vim-fsharp'
Plug 'elixir-lang/vim-elixir'
Plug 'fatih/vim-go'
Plug 'eagletmt/ghcmod-vim'
Plug 'pangloss/vim-javascript'
Plug 'kchmck/vim-coffee-script'
Plug 'jwhitley/vim-literate-coffeescript'
Plug 'noc7c9/vim-iced-coffee-script'
Plug 'mxw/vim-jsx'
Plug 'flowtype/vim-flow'
Plug 'gkz/vim-ls'
Plug 'elmcast/elm-vim'
Plug 'tpope/vim-haml'
Plug 'digitaltoad/vim-jade'
Plug 'slim-template/vim-slim'
Plug 'lunaru/vim-less'
Plug 'tpope/vim-liquid'
Plug 'docker/docker', {'rtp': '/contrib/syntax/vim/'}
Plug 'rodjek/vim-puppet'
Plug 'tpope/vim-cucumber'

" Colour parentheses according to depth
Plug 'kien/rainbow_parentheses.vim'
" Colour text denoting a CSS colour as itself
Plug 'ap/vim-css-color'

" Git support
Plug 'tpope/vim-fugitive'
" Highlight errors
Plug 'scrooloose/syntastic'
" Search across the JVM classpath
Plug 'tpope/vim-classpath'
" Clojure REPL
Plug 'tpope/vim-fireplace'

" Edit encrypted files
Plug 'jamessan/vim-gnupg'

call plug#end()

" In Neovim, <C-h> generates a backspace. We want it to do the right thing.
nmap <BS> <C-h>

" Yank, without jank.
vnoremap <expr>y "my\"" . v:register . "y`y"

" Search for two characters with the ' key.
nmap ' <Plug>(easymotion-s2)
vmap ' <Plug>(easymotion-s2)

autocmd BufWritePre * :%s/\s\+$//e " Delete trailing spaces
autocmd FileWritePre,BufWritePre *.go GoImports

" Show tabs, trailing spaces and lines that extend past the right of the screen
" Disable for text and Markdown files
set list!
set listchars=tab:\ \ ,trail:·,extends:>
autocmd Filetype text setlocal nolist
autocmd Filetype markdown setlocal nolist colorcolumn=0

" Show/hide NERDTree with <§>, and find the current file with <±>
map § :NERDTreeToggle<CR>
map ± :NERDTreeFind<CR>

" Set up Deoplete with Tab-completion
let g:deoplete#enable_at_startup = 1
inoremap <expr><tab> pumvisible() ? "\<c-n>" : "\<tab>"

if executable('ag')
    " Use ag for :grep and :Ack.
    set grepprg=ag\ --nogroup\ --nocolor
    let g:ackprg = 'ag --vimgrep'

    " Alias :Ag as :Ack.
    command -nargs=* Ag Ack <args>

    " Use ag in CtrlP for listing files.
    let g:ctrlp_user_command = 'ag %s -l --nocolor -g ""'
    let g:ctrlp_use_caching = 0 " ag is fast enough that CtrlP doesn't need to cache.
endif

" Set up Solarized Dark
set background=dark
colorscheme solarized

" Set the font
if has("mac")
    try
        set guifont=Source\ Code\ Pro\ Light:h14
    catch
        set guifont=Menlo:h14
    endtry
elseif has("unix")
    try
        set guifont=Source\ Code\ Pro\ Light:h14
    catch
        try
            set guifont=Inconsolata\ 12
        catch
        endtry
    endtry
elseif has("win32")
    set guifont=Consolas:h12
endif

autocmd BufLeave,FocusLost * nested silent! update " Autosave the file when losing focus or leaving the buffer.
let g:tmux_navigator_save_on_switch = 1

let g:vim_markdown_folding_disabled = 1
let g:jsx_ext_required = 0
let g:flow#autoclose = 1
let g:elm_setup_keybindings = 0

let g:go_highlight_functions = 1
let g:go_highlight_methods = 1
let g:go_highlight_fields = 1
let g:go_highlight_types = 1
let g:go_highlight_operators = 1
let g:go_highlight_build_constraints = 1

" I have no idea why Syntastic thinks that `<%=` doesn't use the variable.
let g:syntastic_eruby_ruby_quiet_messages = {'regex': 'possibly useless use of a variable in void context'}

autocmd BufNewFile,BufRead Guardfile,*.Guardfile set filetype=ruby
autocmd BufNewFile,BufRead *.ru set filetype=ruby
autocmd BufNewFile,BufRead *.gradle set filetype=groovy
autocmd BufNewFile,BufRead *.stylus set filetype=sass

autocmd Filetype clojure setlocal sw=2 ts=2 sts=2
autocmd Filetype coffee,coffee.iced-coffee,litcoffee setlocal sw=2 ts=2 sts=2
autocmd Filetype elm setlocal sw=2 ts=2 sts=2
autocmd Filetype haskell setlocal sw=2 ts=2 sts=2
autocmd Filetype jade setlocal sw=2 ts=2 sts=2
autocmd Filetype ruby setlocal sw=2 ts=2 sts=2
autocmd Filetype scala setlocal sw=2 ts=2 sts=2
autocmd Filetype slim setlocal sw=2 ts=2 sts=2
autocmd Filetype yaml setlocal sw=2 ts=2 sts=2

autocmd VimEnter *.clj RainbowParenthesesToggle
autocmd Syntax *.clj RainbowParenthesesLoadRound
autocmd Syntax *.clj RainbowParenthesesLoadSquare
autocmd Syntax *.clj RainbowParenthesesLoadBraces

autocmd FileWritePost,BufWritePost * call Autoformat()
autocmd FileWritePost,BufWritePost * call CheckForErrors()

function! ClearHighlights()
    if &ft == 'haskell'
        GhcModTypeClear
    endif
endfunction

" Set up shortcuts for running the build and tests.
" This depends on the user running the `watch-it` script in another window.
function! WriteToWatchFile(commands)
    let file = substitute(system("tmux display-message -p '/tmp/watching-it-#{session_id}-#{window_index}'"), '[[:cntrl:]]', '', 'g')
    update
    call writefile(['set -e'] + a:commands, file)
endfunction

function! Build()
    if exists('g:build_commands')
        let commands = g:build_commands
    elseif file_readable('Makefile')
        let commands = ['make']
    endif
    if exists('commands')
        call WriteToWatchFile(commands)
    else
        echoerr("I don't know how to build this repository.")
    endif
endfunction

function! RunTest()
    if exists('g:test_commands')
        let commands = g:test_commands
    elseif file_readable('stack.yaml')
        let commands = ['stack test']
    elseif file_readable('Setup.hs')
        let commands = ['cabal build', 'cabal test']
    elseif file_readable('Rakefile')
        let commands = ['rake']
    elseif file_readable('Gemfile')
        let commands = ['rspec --color']
    elseif file_readable('package.json')
        let commands = ['npm test']
    elseif file_readable('Makefile')
        let commands = ['if [[ $(set +e; make -q check; echo $?) -ne 2 ]]', 'then', 'make check', 'else', 'make test', 'fi']
    endif
    if exists('commands')
        call WriteToWatchFile(commands)
    else
        echoerr("I don't know how to run the tests for this repository.")
    endif
endfunction

function! CheckForErrors()
    SyntasticCheck
    Errors
endfunction

function! Autoformat()
    if &ft == 'javascript' && executable('standard')
        silent ! standard --fix %<
    endif
endfunction

noremap <leader>c :nohlsearch<CR>:call ClearHighlights()<CR>
noremap <Leader>b :call Build()<CR>
noremap <Leader>t :call RunTest()<CR>
noremap <Leader>p :GhcModType<CR>
noremap <Leader>e :update<CR>:call CheckForErrors()<CR>
noremap <Leader>l :lclose<CR>

let g:GPGPreferSymmetric=1 " Use symmetric GPG encryption with new files
