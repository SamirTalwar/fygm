#!/usr/bin/env zsh

set -e
set -o pipefail

xargs=$(whence gxargs || whence xargs)

echo 'Removing exited processes...'
docker ps --filter='status=exited' --quiet \
  | $xargs --no-run-if-empty docker rm

echo 'Removing dangling images...'
# First, remove dangling images that have been somehow tagged with a repository but no name.
# We do this by re-tagging them and then removing them.
docker images \
  | tail +2 \
  | awk '$1 != "<none>" && $2 == "<none>" { print $1 " " $3 }' \
  | while read repository id; do
    docker tag $id "${repository}:${id}"
    docker rmi "${repository}:${id}"
  done
# Then remove the rest.
docker images --filter=dangling=true --quiet \
  | $xargs --no-run-if-empty docker rmi

echo 'Removing dangling, unnamed volumes...'
docker volume ls --filter=dangling=true --quiet \
  | awk '$0 ~ "^[0-9a-f]{64}$"' \
  | $xargs --no-run-if-empty docker volume rm

created=$(docker ps --filter='status=created')
has_created=$([[ -n $(echo $created | tail -n+2) ]] && echo true || echo false)
dead=$(docker ps --filter='status=dead')
has_dead=$([[ -n $(echo $dead | tail -n+2) ]] && echo true || echo false)

if $has_created; then
  echo
  echo 'WARNING: Some processes have been created but not started.'
  echo $created
fi

if $has_dead; then
  echo
  echo 'WARNING: Some processes have died.'
  echo $dead
fi

if ! $has_created && ! $has_dead; then
  echo 'Your Docker is now sparkling.'
fi
