#!/usr/bin/env zsh

[[ -s "$HOME/.rvm/scripts/rvm" ]] && \
    source "$HOME/.rvm/scripts/rvm"
[[ -s "$HOME/.sdkman/bin/sdkman-init.sh" ]] && \
    export SDKMAN_DIR="$HOME/.sdkman" && \
    source "$HOME/.sdkman/bin/sdkman-init.sh"

set -e

ALL_PACKAGE_MANAGERS=(
    brew
    apt
    sdk
    rvm
    gem
    pip
    npm
    cabal
    raco
    docker
)

function exists {
    (( $+commands[$1] )) || which $1 >/dev/null
}

function update-brew {
    if exists brew; then
        brew update
        brew upgrade --all
        brew cleanup
        brew cask cleanup
        brew linkapps
    fi
}

function update-apt {
    if exists apt-get; then
        apt-get update
        apt-get upgrade
    fi
}

function update-sdk {
    if exists sdk; then
        sdk selfupdate
    fi
}

function update-rvm {
    if exists rvm; then
        rvm get head
        rvm cleanup all
        rvm reload
    fi
}

function update-gem {
    if exists gem; then
        if exists rvm; then
            (rvm use ruby@global && update-all-gems)
        fi

        update-all-gems
    fi
}

function update-all-gems {
    gem update --system
    gem update
    gem cleanup
}

function update-pip {
    if exists pip; then
        pip install --upgrade setuptools
        pip install --upgrade pip
    fi
}

function update-npm {
    if exists npm; then
        npm update -g
        npm upgrade -g
    fi
}

function update-cabal {
    if exists cabal; then
        cabal update
    fi
}

function update-raco {
    if exists raco; then
        raco pkg update --all --auto
    fi
}

function update-docker {
    if exists docker && (! exists docker-machine || [[ -n $DOCKER_MACHINE_NAME && $(docker-machine status $DOCKER_MACHINE_NAME) == 'Running' ]]); then
        docker-cleanup
        if exists docker-machine; then
            docker-machine upgrade
            waituntil --timeout=30 docker ps >/dev/null
        fi
        docker images | tail -n+2 | awk '{ print $1 ":" $2 }' | xargs -n1 docker pull
        docker-cleanup
    fi
}

if [[ $# -eq 0 ]]; then
    package_managers=($ALL_PACKAGE_MANAGERS)
else
    typeset -a package_managers
    i=0
    for package_manager in $@; do
        i=$((i + 1))
        if [[ ${+ALL_PACKAGE_MANAGERS[(r)$package_manager]} -eq 0 ]]; then
            echo >&2 "Cannot update \"$package_manager\"."
            exit 1
        fi
        package_managers[$i]=$package_manager
    done
fi

for package_manager in $package_managers; do
    echo "Updating $package_manager..."
    echo "Updating $package_manager..." | sed 's/./=/g' # Underline the previous statement.
    update-$package_manager
    echo
done
