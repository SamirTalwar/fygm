#!/usr/bin/env zsh

[[ -s "$HOME/.rvm/scripts/rvm" ]] && source "$HOME/.rvm/scripts/rvm"

set -e

ALL_PACKAGE_MANAGERS=(
    brew
    apt
    rvm
    gem
    pip
    npm
    cabal
    docker
)

function exists {
    (( $+commands[$1] ))
}

function update-brew {
    if exists brew; then
        brew update
        brew upgrade --all
        brew cleanup
        brew cask cleanup
        brew linkapps
    fi
}

function update-apt {
    if exists apt-get; then
        apt-get update
        apt-get upgrade
    fi
}

function update-rvm {
    if exists rvm; then
        rvm get head
        rvm cleanup all
        rvm reload
    fi
}

function update-gem {
    if exists gem; then
        if exists rvm; then
            (rvm use ruby@global && update-all-gems)
        fi

        update-all-gems
    fi
}

function update-all-gems {
    gem update --system
    gem update
    gem cleanup
}

function update-pip {
    if exists pip; then
        pip install --upgrade setuptools
        pip install --upgrade pip
    fi
}

function update-npm {
    if exists npm; then
        npm update -g
        npm upgrade -g
    fi
}

function update-cabal {
    if exists cabal; then
        cabal update
    fi
}

function update-docker {
    if exists docker && (! exists boot2docker || [[ $(boot2docker status) == 'running' ]]); then
        docker-cleanup
        docker images | tail -n+2 | awk '{ print $1 ":" $2 }' | xargs -n1 docker pull
        docker-cleanup
    fi
}

if [[ $# -eq 0 ]]; then
    package_managers=($ALL_PACKAGE_MANAGERS)
else
    typeset -a package_managers
    i=0
    for package_manager in $@; do
        i=$((i + 1))
        if [[ ${+ALL_PACKAGE_MANAGERS[(r)$package_manager]} -eq 0 ]]; then
            echo >&2 "Cannot update \"$package_manager\"."
            exit 1
        fi
        package_managers[$i]=$package_manager
    done
fi

for package_manager in $package_managers; do
    echo "Updating $package_manager..."
    echo "Updating $package_manager..." | sed 's/./=/g' # Underline the previous statement.
    update-$package_manager
    echo
done
