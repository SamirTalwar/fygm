#!/usr/bin/env zsh

set -e
set -u

if [[ $# -ne 1 ]]; then
  echo >&2 "Usage: ${0} OUTPUT-FILE"
  exit 2
fi

output_file=$1
if [[ $output_file =~ '\.gpg$' ]]; then
  encrypt=true
else
  encrypt=false
fi

email=$(lpass show --username Simplenote)
password=$(lpass show --password Simplenote)

session_file=$(mktemp session.XXXXXX)
downloaded_file=$(mktemp session.XXXXXX)
function cleanup {
  rm -f $session_file $downloaded_file
}
trap cleanup EXIT

echo >&2 'Signing in...'
http --check-status --follow --session=$session_file --form \
  GET https://app.simplenote.com/signin \
  'Referer: https://app.simplenote.com/' \
  > /dev/null
http --check-status --follow --session=$session_file --form \
  POST https://app.simplenote.com/user \
  'Referer: https://app.simplenote.com/signin' \
  email=$email password=$password \
  > /dev/null

echo >&2 'Starting the export...'
start=$(date '+%s')
while true; do
  response=$(
    http --check-status --session=$session_file \
      GET https://app.simplenote.com/export/start \
      'Accept: application/json, text/javascript, */*; q=0.01' \
      'Referer: https://app.simplenote.com/' \
      'X-Requested-With: XMLHttpRequest'
  )
  if [[ $(jq '.status' <<< $response) -eq 3 ]]; then
    break
  fi
  if [[ $(( $(date '+%s') - start )) -gt 30 ]]; then
    echo >&2 'Timed out.'
    exit 1
  fi
done
echo >&2 'Export finished.'

echo >&2 'Downloading the export...'
http --check-status --session=$session_file \
  --print=b --download --output=$downloaded_file \
  GET https://app.simplenote.com/export/download \
  key==${email} \
  'Referer: https://app.simplenote.com/' \
  'X-Requested-With: XMLHttpRequest' \
  > /dev/null

rm -f $output_file
if $encrypt; then
  gpg -e -r $email -o $output_file $downloaded_file
  rm $downloaded_file
else
  mv $downloaded_file $output_file
fi

echo >&2 "Exported to ${output_file}."
