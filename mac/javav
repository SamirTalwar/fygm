#!/usr/bin/env zsh

function version_should_match {
    echo >&2 'The version should match an installed Java VM.'
}

if [[ $# -eq 0 ]]; then
    echo >&2 "Usage:"
    echo >&2 "    source $0 <version>"
    echo >&2 "    $0 <version> <command>"
    echo >&2
    version_should_match
    return 1
fi

version=$1
shift
cmd=($@)

function java_home {
    /usr/libexec/java_home $@ 2>/dev/null
}

if [[ ! $version =~ ^[0-9\.]+$ ]]; then
    echo >&2 'The version must be a number of the form "9" or "1.9".'
    return 2
fi

if JAVA_HOME=$(java_home -F -v $version); then
elif JAVA_HOME=$(java_home -F -v 1.$version); then
else
    version_should_match
    return 3
fi
export JAVA_HOME

path=($JAVA_HOME/bin $path)
export PATH

if [[ $#cmd -gt 0 ]]; then
    $cmd
fi
